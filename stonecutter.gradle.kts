plugins {
    id("dev.kikugie.stonecutter")
}

stonecutter active "1.21.3" // [ACTIVE_VERSION]

stonecutter parameters {
    constants.put("UNOBFUSCATED", node.metadata.project.startsWith("26."))
    constants.put("SODIUM", true)
    // VulkanMod is an optional, opt-in renderer backend. Keep this set in sync with the versions
    // whose gradle.properties define `deps.vulkanmod` (that property drives the build.gradle
    // dependency + mixin-overlay gating; this constant drives the //? if VULKANMOD source guards).
    // 0.5.x API: 1.21.2-1.21.5. 0.6.x API: 1.21, 1.21.1, 1.21.9-1.21.11.
    // (1.21.6-1.21.8 absent — VulkanMod never released for them.)
    // 26.1.x (unobfuscated line) supported via VulkanMod 0.6.8 = CEmnv55N (covers 26.1/26.1.1/26.1.2).
    constants.put("VULKANMOD", node.metadata.project in setOf(
        "1.21", "1.21.1", "1.21.2", "1.21.3", "1.21.4", "1.21.5",
        "1.21.9", "1.21.10", "1.21.11",
        "26.1", "26.1.1", "26.1.2"
    ))
}

// Register version switch tasks
for (ver in stonecutter.versions) {
    val taskName = "stonecutterSwitchTo${ver.project}"
    if (tasks.findByName(taskName) == null) {
        tasks.register(taskName) {
            group = "stonecutter"
            description = "Switch active version to ${ver.project}"
            doLast {
                val marker = "[ACTIVE_VERSION]"
                val script = project.file("stonecutter.gradle.kts")
                val lines = script.readLines().toMutableList()
                for (i in lines.indices) {
                    if (lines[i].contains(marker)) {
                        lines[i] = "stonecutter active \"${ver.project}\" // $marker"
                        break
                    }
                }
                script.writeText(lines.joinToString("\n") + "\n")
                println("Switched active version to ${ver.project}. Reload Gradle to apply.")
            }
        }
    }
}
